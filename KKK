IS_Meta =
DATATABLE(
    "AuditGroup", STRING,
    "Section", STRING,
    "LineType", STRING,
    "SortOrder", INTEGER,
    {
        { "Local Network Services", "Operating Revenues", "Revenue", 1 },
        { "Network Access Services", "Operating Revenues", "Revenue", 2 },
        { "Long Distance Services", "Operating Revenues", "Revenue", 3 },
        { "Internet, Sales and Other Services", "Operating Revenues", "Revenue", 4 },
        { "Miscellaneous Revenue", "Operating Revenues", "Revenue", 5 },
        { "Plant Specific Operations", "Operating Expenses", "Expense", 6 },
        { "Plant Nonspecific Operations", "Operating Expenses", "Expense", 7 },
        { "Cost of Long Distance Services", "Operating Expenses", "Expense", 8 },
        { "Cost of Internet, Sales and Other Services", "Operating Expenses", "Expense", 9 },
        { "Depreciation", "Operating Expenses", "Expense", 10 },
        { "Customer Operations", "Operating Expenses", "Expense", 11 },
        { "Corporate Operations", "Operating Expenses", "Expense", 12 },
        { "General Taxes", "Operating Expenses", "Expense", 13 },
        { "Interest and Dividend Income", "Other Income", "OtherIncome", 14 },
        { "Earnings from Equity Method Investments", "Other Income", "OtherIncome", 15 },
        { "Other, Net", "Other Income", "OtherIncome", 16 },
        { "Income Tax Expense", "Income Taxes", "Tax", 17 }
    }
)


-- transaction sign
NetAmount_Tel :=
SUM ( 'tblGL (Telephone)'[dblDebit] ) - SUM ( 'tblGL (Telephone)'[dblCredit] )

-- leaf value for the matrix; respects the Year slicer
IS Base (Tel) :=
[NetAmount_Tel]

-- section totals for the “bottom block”
IS Revenues (Tel) :=
CALCULATE ( [IS Base (Tel)], REMOVEFILTERS(tblAuditGroup), KEEPFILTERS( IS_Meta[LineType] = "Revenue" ) )

IS Expenses (Tel) :=
CALCULATE ( [IS Base (Tel)], REMOVEFILTERS(tblAuditGroup), KEEPFILTERS( IS_Meta[LineType] = "Expense" ) )

IS Other Income (Tel) :=
CALCULATE ( [IS Base (Tel)], REMOVEFILTERS(tblAuditGroup), KEEPFILTERS( IS_Meta[LineType] = "OtherIncome" ) )

IS Income Tax (Tel) :=
CALCULATE ( [IS Base (Tel)], REMOVEFILTERS(tblAuditGroup), KEEPFILTERS( IS_Meta[LineType] = "Tax" ) )

IS Equity Earnings (Tel) :=
CALCULATE ( [IS Base (Tel)], REMOVEFILTERS(tblAuditGroup),
            KEEPFILTERS( IS_Meta[AuditGroup] = "Equity Earnings of Subsidiary" ) )

-- audit-style calculations
IS Operating Margin (Tel) :=
[IS Revenues (Tel)] - [IS Expenses (Tel)]

IS Margins Before Income Taxes (Tel) :=
[IS Operating Margin (Tel)] + [IS Other Income (Tel)]

IS Margins Before Equity Loss of Subsidiary (Tel) :=
[IS Margins Before Income Taxes (Tel)] - [IS Income Tax (Tel)]

IS Net Margins (Tel) :=
[IS Margins Before Equity Loss of Subsidiary (Tel)] + [IS Equity Earnings (Tel)]


IS BottomValues_Tel :=
VAR lbl = SELECTEDVALUE ( IS_BottomRows[Label] )
RETURN
SWITCH (
    lbl,
    "Operating Margins",                        [IS Operating Margin (Tel)],
    "Margins Before Income Taxes",              [IS Margins Before Income Taxes (Tel)],
    "Margins Before Equity Loss of Subsidiary", [IS Margins Before Equity Loss of Subsidiary (Tel)],
    "Equity Earnings of Subsidiary",            [IS Equity Earnings (Tel)],
    "Net Margins",                              [IS Net Margins (Tel)],
    BLANK()
)

